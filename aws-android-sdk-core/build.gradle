//apply from: rootProject.file('publishing.gradle')
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 29

        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.8.9'
    implementation 'androidx.annotation:annotation:1.1.0'

    testImplementation 'joda-time:joda-time:2.8.1'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'org.easymock:easymock:3.1'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'xerces:xercesImpl:2.12.0'
}

ArrayList<String> pubs = new ArrayList<>()

afterEvaluate {
    android.libraryVariants.all { variant ->
        if (variant.name == "release") {
            pubs.add("release")
            publishing {
                publications {
                    aar(MavenPublication) {
                        groupId GROUP
                        artifactId POM_ARTIFACT_ID
                        version VERSION_NAME
                        artifact("${buildDir}/outputs/aar/${artifactId}-release.aar")
                        pom.withXml {
                            def dependenciesNode = asNode().appendNode('dependencies')
                            configurations.implementation.allDependencies.each {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
            artifactoryPublish {
                publications(publishing.publications.aar)
            }
        }
    }
}

artifactory {
    contextUrl = 'http://mercurial.objectway.it/artifactory'
    publish {
        repository {
            repoKey = 'ext-release-local'

            username = "$artifactory_username"
            password = "$artifactory_password"
        }
        defaults {
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}