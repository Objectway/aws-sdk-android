//apply from: rootProject.file('publishing.gradle')
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16 // androidx.browser
        targetSdkVersion 29

        manifestPlaceholders = [
                'authRedirectScheme': 'FOR_LIBRARY_CONSUMER_TO_OVERRIDE'
        ]
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    /*api (project(':aws-android-sdk-core')) {
        exclude group: 'com.google.android', module: 'android'
    }
    api project(':aws-android-sdk-cognitoidentityprovider-asf')*/
    implementation 'androidx.browser:browser:1.3.0'

    api "com.amazonaws:aws-android-sdk-core:$VERSION_NAME"
    api "com.amazonaws:aws-android-sdk-cognitoidentityprovider-asf:$VERSION_NAME"
}

ArrayList<String> pubs = new ArrayList<>()

afterEvaluate {
    android.libraryVariants.all { variant ->
        if (variant.name == "release") {
            pubs.add("release")
            publishing {
                publications {
                    aar(MavenPublication) {
                        groupId GROUP
                        artifactId POM_ARTIFACT_ID
                        version VERSION_NAME
                        artifact("${buildDir}/outputs/aar/${artifactId}-release.aar")
                        pom.withXml {
                            def dependenciesNode = asNode().appendNode('dependencies')
                            configurations.implementation.allDependencies.each {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
            artifactoryPublish {
                publications(publishing.publications.aar)
            }
        }
    }
}

artifactory {
    contextUrl = 'http://mercurial.objectway.it/artifactory'
    publish {
        repository {
            repoKey = 'ext-release-local'

            username = "$artifactory_username"
            password = "$artifactory_password"
        }
        defaults {
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}